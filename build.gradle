/*
 * Adapt is Copyright (c) 2021 Arcane Arts (Volmit Software)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'java-library'
    id "io.freefair.lombok" version "8.10.2"
    id "com.gradleup.shadow" version "8.3.5"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.9"
}

version '1.16.7'
def apiVersion = '1.21'
def name = getRootProject().getName() // Defined in settings.gradle
def main = 'com.volmit.adapt.Adapt'
def manifoldVersion = '2024.1.49'

/**
 * Gradle is weird sometimes, we need to delete the plugin yml from the build folder to actually filter properly.
 */
file(jar.archiveFile.get().getAsFile().getParentFile().getParentFile().getParentFile().getAbsolutePath() + '/build/resources/main/plugin.yml').delete()

/**
 * Expand properties into plugin yml
 */

processResources {
    filesMatching('**/plugin.yml') {
        expand(
                'name': name.toString(),
                'version': version.toString(),
                'main': main.toString(),
                'apiversion': apiVersion.toString()
        )
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.papermc.io/repository/maven-public/"}
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    maven { url "https://nexus.frengor.com/repository/public/"}
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url "https://repo.glaremasters.me/repository/bloodshot/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://repo.oraxen.com/releases" }
    maven { url "https://repo.alessiodp.com/releases" }
    maven { url 'https://jitpack.io' }
}

/**
 * We need parameter meta for the decree command system
 */
compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << '-parameters' << '-Xplugin:Manifold'
    options.annotationProcessorPath = configurations.annotationProcessor
}

dependencies {
    // Provided or Classpath
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    // Cancer
    implementation 'com.github.VolmitDev:Fukkit:23.6.1'
    implementation 'com.github.VolmitDev:Amulet:23.5.1'
    implementation 'com.github.VolmitDev:Chrono:22.9.10'
    implementation 'com.github.VolmitDev:Spatial:22.11.1'
    implementation 'net.byteflux:libby-velocity:1.3.1'

    implementation "io.papermc:paperlib:1.0.7"
    annotationProcessor 'systems.manifold:manifold-ext:' + manifoldVersion
    testAnnotationProcessor 'systems.manifold:manifold-ext:' + manifoldVersion
    implementation 'systems.manifold:manifold-rt:' + manifoldVersion

    compileOnly 'me.clip:placeholderapi:2.11.6'
    implementation 'xyz.xenondevs:particle:1.8.4'
    implementation "com.frengor:ultimateadvancementapi-shadeable:2.4.3"
    implementation 'com.jeff-media:custom-block-data:2.2.3'
    compileOnly 'io.netty:netty-all:4.1.68.Final'

    // Dynamically Loaded
    compileOnly 'me.xiaozhangup.octopus:octopus-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'me.xiaozhangup:SlimeCargoNext:1.0.1'
    compileOnly 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'com.google.code.gson:gson:2.10'
    compileOnly 'com.elmakers.mine.bukkit:EffectLib:10.4'
    compileOnly 'net.kyori:adventure-text-minimessage:4.18.0'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.3.4'
    compileOnly 'it.unimi.dsi:fastutil:8.5.13'
    compileOnly "fr.skytasul:glowingentities:1.4.3"
    compileOnly 'com.google.guava:guava:30.1-jre'
    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.0.6'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

/**
 * Configure Adapt for shading
 */
shadowJar {
    append("plugin.yml")
    relocate 'manifold', 'com.volmit.adapt.util.manifold'
    relocate 'art.arcane', 'com.volmit.adapt.util.arcane'
    relocate 'Fukkit.extensions', 'com.volmit.adapt.util.extensions'
    relocate 'Amulet.extensions', 'com.volmit.adapt.util.extensions'
    relocate 'com.fren_gor.ultimateAdvancementAPI', 'com.volmit.adapt.util.advancements'
    relocate 'net.byteflux.libby', 'com.volmit.adapt.util.libby'
    relocate 'com.jeff_media.customblockdata', 'com.volmit.util.customblocks'
    relocate 'io.papermc.lib', 'com.volmit.util.paperlib'
    dependencies {
        include(dependency('systems.manifold:'))
        include(dependency('xyz.xenondevs:'))
        include(dependency('com.github.VolmitDev:'))
        include(dependency('com.frengor:ultimateadvancementapi-shadeable:'))
        include(dependency('net.byteflux:'))
        include(dependency('com.jeff-media:custom-block-data:'))
        include(dependency('io.papermc:paperlib:'))
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 60, 'minutes'
    resolutionStrategy.cacheDynamicVersionsFor 60, 'minutes'
}

def buildDir = layout.buildDirectory.asFile.get()
def outputJar = new File(buildDir, "libs/Adapt-" + version + "-all.jar")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.build.dependsOn(shadowJar)

task adapt(type: Copy)
{
    group "adapt"
    from outputJar
    into buildDir
    rename { String fileName ->
        fileName.replace(outputJar.getName(), "Adapt-" + version + ".jar")
    }
    dependsOn(build)
}