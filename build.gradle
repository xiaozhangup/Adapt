import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/*
 * Adapt is Copyright (c) 2021 Arcane Arts (Volmit Software)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "2.1.21"
    id "io.freefair.lombok" version "8.10.2"
    id "com.gradleup.shadow" version "8.3.5"
    id 'systems.manifold.manifold-gradle-plugin' version '0.0.2-alpha'
//    id 'com.diffplug.spotless' version '7.0.4'
}

version '1.16.7'
def apiVersion = '1.21'
def name = getRootProject().getName() // Defined in settings.gradle
def main = 'com.volmit.adapt.Adapt'

/**
 * Gradle is weird sometimes, we need to delete the plugin yml from the build folder to actually filter properly.
 */
file(jar.archiveFile.get().getAsFile().getParentFile().getParentFile().getParentFile().getAbsolutePath() + '/build/resources/main/plugin.yml').delete()

/**
 * Expand properties into plugin yml
 */

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "21"
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

processResources {
    filesMatching('**/plugin.yml') {
        expand(
                'name': name.toString(),
                'version': version.toString(),
                'main': main.toString(),
                'apiversion': apiVersion.toString()
        )
    }
}

manifold {
    manifoldVersion = '2025.1.27'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    maven { url "https://nexus.frengor.com/repository/public/" }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url "https://repo.glaremasters.me/repository/bloodshot/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://repo.momirealms.net/releases/" }
    maven { url "https://repo.oraxen.com/releases" }
    maven { url "https://repo.alessiodp.com/releases" }
    maven { url 'https://jitpack.io' }
}

//spotless {
//    java {
//        eclipse()
//        target("src/**/*.java")
//        leadingTabsToSpaces(4)
//    }
//}

dependencies {
    // Provided or Classpath
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation "systems.manifold:manifold-ext-rt:${manifold.manifoldVersion.get()}"
    annotationProcessor "systems.manifold:manifold-ext:${manifold.manifoldVersion.get()}"

    compileOnly 'me.clip:placeholderapi:2.11.6'
    implementation 'xyz.xenondevs:particle:1.8.4'
    implementation 'com.frengor:ultimateadvancementapi-shadeable:2.6.0:mojang-mapped'
    implementation 'com.jeff-media:custom-block-data:2.2.3'

    // Dynamically Loaded
    compileOnly 'me.xiaozhangup.octopus:octopus-api:1.21.8-R0.1-SNAPSHOT'
    compileOnly 'me.xiaozhangup:SlimeCargoNext:1.0.1'
    compileOnly 'me.xiaozhangup:OrangDomain:1.0.1'
    compileOnly 'me.xiaozhangup:WhaleMechanism:1.0.1'
    compileOnly 'net.momirealms:craft-engine-core:0.0.57.1'
    compileOnly 'net.momirealms:craft-engine-bukkit:0.0.57.1'
    compileOnly 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'com.google.code.gson:gson:2.10'
    compileOnly 'com.elmakers.mine.bukkit:EffectLib:10.4'
    compileOnly 'com.google.guava:guava:30.1-jre'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 60, 'minutes'
    resolutionStrategy.cacheDynamicVersionsFor 60, 'minutes'
}

/**
 * We need parameter meta for the decree command system
 */
compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

/**
 * Configure Adapt for shading
 */
shadowJar {
    append("plugin.yml")
    relocate 'manifold', 'com.volmit.adapt.util.manifold'
    relocate 'com.fren_gor.ultimateAdvancementAPI', 'com.volmit.adapt.util.advancements'
    relocate 'com.jeff_media.customblockdata', 'com.volmit.adapt.util.customblocks'
    relocate 'xyz.xenondevs.particle', 'com.volmit.adapt.util.particle'
    dependencies {
        include(dependency('systems.manifold:'))
        include(dependency('xyz.xenondevs:'))
        include(dependency('com.frengor:ultimateadvancementapi-shadeable:'))
        include(dependency('net.byteflux:'))
        include(dependency('com.jeff-media:custom-block-data:'))
    }
}

def buildDir = layout.buildDirectory.asFile.get()
def outputJar = new File(buildDir, "libs/Adapt-" + version + "-all.jar")

tasks.build.dependsOn(shadowJar)

task adapt(type: Copy)
{
    group "adapt"
    from outputJar
    into buildDir
    rename { String fileName ->
        fileName.replace(outputJar.getName(), "Adapt-" + version + ".jar")
    }
    dependsOn(build)
}